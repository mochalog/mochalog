/*
 * Copyright 2017 The Mochalog Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java-library'
apply plugin: 'maven-publish'

// Prolog pack versions should not contain SNAPSHOT values
version '0.1.0'

// Publish Prolog sources to Prolog pack in Maven repository
publishing {
    publications {
        "${project.name}"(MavenPublication) {
            artifact buildSourceJar
        }
    }
}

/**
 * Generate a .zip file corresponding to
 * a Prolog pack containing the Mochalog Prolog interface
 */
task buildMochalogPack(type: Zip) {
    from 'src/main/prolog/packs/mochalog'

    // Ensure pack.pl file is correctly
    // updated corresponding to the current version
    filesMatching('pack.pl') {
        expand(version: project.version)
    }
}

/**
 * Generate a .jar file containing io.mochalog.bridge.prolog
 * Java sources and generated Prolog packs
 */
task buildSourceJar(dependsOn: buildMochalogPack, type: Jar) {
    from sourceSets.main.output
    from (buildMochalogPack.outputs.files) {
        into 'packs'
    }

    from ('src/main/prolog/packs/mochalog-version') {
        into 'packs'
        expand(version: project.version)
    }
}