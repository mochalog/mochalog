/*
 * Copyright 2017 The Mochalog Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Sourced from skeleton Gradle Xtext integration

configurations {
    mwe2 {
        extendsFrom compile
    }
}

dependencies {
	compile "org.eclipse.xtext:org.eclipse.xtext:${xtextVersion}"
    compile "org.eclipse.xtext:org.eclipse.xtext.xbase:${xtextVersion}"

	mwe2 "org.eclipse.emf:org.eclipse.emf.mwe2.launch:2.8.3"
    mwe2 "org.eclipse.xtext:org.eclipse.xtext.xtext.generator:${xtextVersion}"

	testCompile "org.eclipse.xtext:org.eclipse.xtext.junit4:${xtextVersion}"
	testCompile "org.eclipse.xtext:org.eclipse.xtext.xbase.junit:${xtextVersion}"
}

/**
 * Xtext Prolog to Java translator
 * Generated as part of Gradle build cycle
 */
task generateXtextLanguage(type: JavaExec) {
    main = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
    classpath = configurations.mwe2

    // Input grammars and generators
    inputs.file "src/main/java/io/mochalog/pl2j/model/GenerateProlog.mwe2"
    inputs.file "src/main/java/io/mochalog/pl2j/model/Prolog.xtext"

    // Source output directory
    outputs.dir "src/main/xtext-gen"

    args += "src/main/java/io/mochalog/pl2j/model/GenerateProlog.mwe2"
    args += "-p"
    args += "rootPath=/${projectDir}/.."
}

generateXtext.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
eclipse.classpath.plusConfigurations += [configurations.mwe2]